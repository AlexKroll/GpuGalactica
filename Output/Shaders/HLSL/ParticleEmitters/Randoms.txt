#include "Math.txt"



inline float SetParticleRandom(float frand)
{
	if (frand > 1.0f)
		frand -= 1.0f;

	float fout = frand;

	frand *= 65535.0f;

	int irand = (int)frand;

	particles[ind].rand_texu_add &= 0xFFFF0000;
	particles[ind].rand_texu_add |= irand;

	return fout;
}


inline float PartRandom()
{
	float frand = GetFloat01FromLowHalf(particles[ind].rand_texu_add);

	frand = RandomTex.SampleLevel(Samp, frand, 0).x + Randoms.x;

	frand = SetParticleRandom(frand);
	return frand;
}


inline float3 PartRandom3()
{
	float frand = GetFloat01FromLowHalf(particles[ind].rand_texu_add);

	float4 frand4 = RandomTex.SampleLevel(Samp, frand, 0);

	frand = frand4.w + Randoms.y;

	frand = SetParticleRandom(frand);

	return frand4.xyz;
}


float RandomF(float fmin, float fmax)
{
	return lerp(fmin, fmax, PartRandom());
}


float RandomF01()
{
	float random = PartRandom();
	return random;
}


int RandomI(int imax)
{
	float F = RandomF01() * (float)imax;
	int I = (int)F;
	return I;
}


float3 RandomSphere(float radius)
{
	float3 pos = PartRandom3();

	pos = normalize(pos*2-1) * radius;
	return pos;
}


float3 RandomSphere(float radius1, float radius2)
{
	float radius = RandomF(radius1, radius2);

	float3 pos = PartRandom3();
	pos = normalize(pos*2-1) * radius;
	return pos;
}


float3 RandomCube(float3 size)
{
	float3 pos = PartRandom3() * size;
	return pos;
}